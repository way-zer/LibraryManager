import org.gradle.api.tasks.bundling.Jar

plugins {
    id 'java'
    id "org.jetbrains.kotlin.jvm" version "1.3.41"
    id "me.qoomon.git-versioning" version "2.1.1"
    id "com.jfrog.bintray" version "1.8.4"
}
apply plugin: "maven-publish"
group 'cf.wayzer'
version '1.4'

gitVersioning.apply{
    tag{
        pattern = "v(?<tagVersion>[0-9].*)"
        versionFormat = "\${tagVersion}"
    }
    commit{
        versionFormat = "\${commit.short}-SNAPSHOT"
    }
}

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    testCompileOnly "org.jetbrains.kotlin:kotlin-stdlib:1.3.41"
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = "sources"
}

task javadocJar(type: Jar) {
    from javadoc.destinationDir
    archiveClassifier = "javadoc"
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            artifact sourceJar
            artifact javadocJar
            groupId rootProject.group
            artifactId rootProject.name
            version rootProject.version
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['MyPublication']
    dryRun = false
    override = true
    publish = true
    pkg{
        repo = "maven"
        name = rootProject.name
        desc = 'A library helps you manage jvm runtime dependencies'
        githubRepo = 'way-zer/LibraryManager'
        githubReleaseNotesFile = 'README.md'
        websiteUrl = "https://github.com/${githubRepo}"
        issueTrackerUrl = "https://github.com/${githubRepo}/issues"
        vcsUrl = "https://github.com/${githubRepo}.git"
        licenses = ['MIT']
    }
}
